plugins {
    id 'com.android.library'
    id 'maven-publish'
}

afterEvaluate {
    publishing {
        publications {
            // 这个mavenJava可以随便填，只是一个任务名字而已
            // MavenPublication必须有，这个是调用的任务类
            mavenJava(MavenPublication) {
                // 这里头是artifacts的配置信息，不填会采用默认的
                groupId = 'com.github.timekettledev'
                artifactId = 'EasyBleX'
                version = '1.5.1'
                artifact androidSourcesJar //打包源码，去除这行打的包将看不到源码
            }
        }
    }
}
// 用于打包源代码的任务
tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

android {
    namespace 'cn.wandersnail.ble'

    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        consumerProguardFiles 'easyblex-consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        abortOnError false
        disable 'TypographyFractions', 'TypographyQuotes'
    }
}

dependencies {
    compileOnly 'androidx.appcompat:appcompat:1.3.1'
    compileOnly 'cn.wandersnail:commons-android:1.3.4'
}
